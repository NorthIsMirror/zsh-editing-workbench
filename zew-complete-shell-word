emulate -LR zsh
setopt typesetsilent extendedglob noshortloops

# Prepare output variables for zew-process-buffer
local ZEW_PB_WORDS ZEW_PB_WORDS_BEGINNINGS ZEW_PB_SPACES 
local ZEW_PB_SELECTED_WORD ZEW_PB_LEFT ZEW_PB_RIGHT

autoload zew-process-buffer
zew-process-buffer "$BUFFER"

typeset -g __zew_csw_index __zew_csw_left __zew_csw_right
typeset -ga __zew_csw_found
__zew_csw_found=( "$reply[@]" )

# Consecutive call?
if [ "${WIDGET%-backwards}" = "${LASTWIDGET%-backwards}" ]; then
    if [[ "$WIDGET" != *-backwards ]]; then
        (( __zew_csw_index ++ ))
    else
        (( __zew_csw_index -- ))
    fi
else
    if [[ "$WIDGET" != *-backwards ]]; then
        __zew_csw_index="1"
    else
        # Will get changed into $to_display limit
        __zew_csw_index="0"
    fi
    __zew_csw_left="$ZEW_PB_LEFT"
    __zew_csw_right="$ZEW_PB_RIGHT"
    __zew_csw_found=( )
fi

# Find history words matching $left ... $right
if [ "$#__zew_csw_found" -eq "0" ]; then
    typeset -U __zew_csw_found
    repeat 1; do
        __zew_csw_found=( "${(@M)historywords:#(#i)$__zew_csw_left*$__zew_csw_right}" )
        # Remember for consecutive calls
        reply=( "$__zew_csw_found[@]" )
    done
fi

# Guard values of the index
integer to_display=$(( LINES / 2 ))
[ "$to_display" -gt "$#__zew_csw_found" ] && to_display="$#__zew_csw_found"
[ "$__zew_csw_index" -le 0 ] && __zew_csw_index="$to_display"
[ "$__zew_csw_index" -gt "$to_display" ] && __zew_csw_index=1

# Display matches
typeset -a disp_list
disp_list=( "${(@)__zew_csw_found[1,to_display]}" )
disp_list[__zew_csw_index]="> ${disp_list[__zew_csw_index]} <"
zle -M -- "${(F)disp_list}"

# Regenerate command line
buf=""
integer nwords="${#ZEW_PB_WORDS}"
integer newcursor=0
for (( i=1; i<=nwords; i++ )); do
    if [ "$i" = "$ZEW_PB_SELECTED_WORD" ]; then
        buf+="${ZEW_PB_SPACES[i]}${__zew_csw_found[__zew_csw_index]}"
        newcursor="$#buf"
    else
        buf+="${ZEW_PB_SPACES[i]}${ZEW_PB_WORDS[i]}"
    fi
done

# Set command line
BUFFER="$buf"
# Move cursor to the end of word
CURSOR="$newcursor"

# vim:ft=zsh
